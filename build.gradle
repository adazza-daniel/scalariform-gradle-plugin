defaultTasks 'build'

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'nl.javadude.gradle.plugins'
version = "0.0.1-SNAPSHOT"

repositories { mavenCentral() }


dependencies {
    compile "org.scala-lang:scala-library:2.10.1"
    compile "org.scalariform:scalariform_2.10:0.1.4"
    compile gradleApi()
    testCompile 'junit:junit:4.11'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts { archives sourcesJar, javadocJar }

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    dependsOn signArchives
    doFirst {
        repositories.mavenDeployer {
            uniqueVersion = false
            beforeDeployment { org.gradle.api.artifacts.maven.MavenDeployment deployment -> signing.signPom(deployment) }

            //repository(url: "file://localhost/${rootProject.rootDir}/repo")

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'scalariform-gradle-plugin'
                description 'Applies scalariform to all scala files in your project'
                url 'https://github.com/hierynomus/scalariform-gradle-plugin'
                inceptionYear '2013'

                scm { url 'https://github.com/hierynomus/scalariform-gradle-plugin' }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'hierynomus'
                        name 'Jeroen van Erp'
                        url 'http://www.javadude.nl/'
                        email 'jeroen@javadude.nl'
                        roles { role 'Developer' }
                    }
                }
            }
        }
    }
}

